PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX go: <http://www.geneontology.org/formats/oboInOwl#>
SELECT *
#This query goes through and finds all labels and synomyms for entities available in MONDO and HP.
WHERE {
  #Only select terms defined in MONDO or HP
  ?term rdfs:isDefinedBy ?onto .
  FILTER( ?onto=obo:hp.owl || ?onto=obo:mondo.owl )
  
  #Only select those relatationships which are rdfs:label or a go defined "Synonym" term.
  ?term ?pred ?obj .
  FILTER((STRSTARTS(STR(?pred), STR(go:)) && CONTAINS(STR(?pred), STR("Synonym"))) || (?pred = rdfs:label) ) 
  
  #Don't include deprecated terms.
  FILTER NOT EXISTS {
    ?term owl:deprecated true .
  }
}
#LIMIT 10
